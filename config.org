
#+TITLE: Configuración Emacs
#+author: Álvaro González (@alvarogonzalez)
#+OPTIONS: toc:2 h:4 author:t


#+latex: \newpage
#+html: <div style="margin:auto; text-align:center; font-size:1.5em">
#+latex: \begin{center}
La última versión de esta configuración puede encontrarse en [[https://github.com/alvarogonzalezsotillo/.emacs.d][https://github.com/alvarogonzalezsotillo/.emacs.d]].
#+latex: \end{center}
#+html: </div>

* Cómo funciona este fichero

La configuración de Emacs se realiza con código elisp. Al contrario que otros editores, que están pensados para ser usados sin demasiada customización, los usuarios de Emacs solemos cambiarlo de forma bastante /extrema/ (¡por eso nos gusta!).

El problema es que, como con cualquier otro programa que se va modificando durante años, acabas olvidando el por qué de ciertas líneas de código, o dónde se realiza alguna coniguración. Para evitar remediarlo, se puede utilizar org-mode para crear al mismo tiempo la documentación y el código de la configuración (como en literate programming). Descubrí en el blog de  [[http://pages.sachachua.com/.emacs.d/Sacha.html][Sacha Chua]] que podía mantener la configuración en un fichero =orgmode= y así documentar fácilmente cada opción.

Cada bloque de código de tipo =emacs-lisp= se ejecuta al inicio. Algunos bloques está deshabilitados, marcándolos con el tipo =lisp=. Así mantienen su apariencia, pero no son interpretados.

** =init.el=
Emacs comienza cargando el fichero =~/.emacs.d/init.el=. Este fichero simplemente inicializa el sistema de paquetes y carga el paquete =org=, que es el que permite a Emacs manejar este tipo de ficheros. Después, carga este fichero interpretando los bloques de código.

#+include: "~/.emacs.d/init.el" src lisp

** Algunos problemas
- A veces, se empieza usando una versión de =org= (de gnu), y después se  utiliza la del repositorio de orgmode, lo que puede dar problemas. En ese caso, se necesitan dos reinicios.
- Los ficheros =org= dan problemas sin tienen un =title:= definido y la versión de =org= instalada no se recompiló. Para solucionarlo:
  #+begin_src sh
  rm $(find ~/.emacs.d/elpa | grep .elc$)
  #+end_src
  Después es necesario volver a compilar los ficheros
  #+BEGIN_SRC lisp
    (byte-recompile-directory (expand-file-name "~/.emacs.d/elpa") 0 t)
  #+END_SRC

- El fichero de /customize/ lo mantengo aparte del =init.el=, para separar entornos y mejor integración con el control de versiones.
  #+begin_src emacs-lisp
  (setq custom-file "~/.emacs.d/custom-file.el")
  #+end_src



* Optimizaciones
  #+begin_src emacs-lisp
;; Disabling bidirectional text rendering gives a performance boost.
(setq-default bidi-display-reordering 'left-to-right
              bidi-paragraph-direction 'left-to-right)
(setq bidi-inhibit-bpa t)

;; Increase memory usage, but increase performance. No thank you, font compacting!
(setq inhibit-compacting-font-caches t)

;; Here's where we handle the GC
(use-package gcmh
  :ensure t
  :config
  (setq gcmh-idle-delay 5
        gc-cons-threshold (* 500 1024 1024) 
        gcmh-high-cons-threshold (* 1000 1024 1024))
  (gcmh-mode 1))

  #+end_src

* Utilidades externas al /PATH/
En el directorio =~/.emacs.d/bin/= guardo algunas utilidades que me interesa tener en todos los entornos de trabajo. Lo siguiente es para que estén disponibles desde las /shells/ que arranque desde emacs.

Añado también el directorio de =cargo= para =rust=.
#+begin_src emacs-lisp

(let 
  (  
     (exec-path-separator  (if (string-equal system-type "windows-nt") ";" ":"))
     (paths `(
              ,(concat (expand-file-name user-emacs-directory) "bin")
              ,(concat (expand-file-name "~/") ".cargo/bin")
              "/opt/scala/bin" ))
  )
  (dolist (path paths)
    (message "path: %s" path)
    (setenv "PATH" (concat path exec-path-separator (getenv "PATH")))
    (add-to-list 'exec-path path)
  )

  (message "PATH %s" (getenv "PATH"))
  (message "exec-path %s" exec-path)
)

#+end_src

#+RESULTS:
: exec-path (/opt/scala/bin /home/alvaro/.emacs.d/bin /home/alvaro/.local/bin /home/alvaro/.cargo/bin /usr/local/sbin /usr/local/bin /usr/sbin /usr/bin /sbin /bin /usr/games /usr/local/games /snap/bin /usr/local/libexec/emacs/28.0.50/x86_64-pc-linux-gnu)



Las utilidades pueden consultarse en el [[https://github.com/alvarogonzalezsotillo/.emacs.d/tree/master/bin][repositorio en github]], y son:
#+begin_src shell :exports results
ls ~/.emacs.d/bin
#+end_src

#+RESULTS:
| addtopath.sh                 |
| colores-terminal.sh          |
| colores-tty.sh               |
| disable-gnome-animations.sh  |
| ec                           |
| ghpages.sh                   |
| gifttolatex                  |
| githubclone.sh               |
| install-emacs-as-service.org |
| invert.sh                    |
| keymon.sh                    |
| pdf2svg.sh                   |
| plantuml.1.2018.11.jar       |
| rmtemplatex.sh               |
| rotate-screen.sh             |
| shellcheck                   |
| shrinkpdf.sh                 |
| svg2pdf.sh                   |
| texlab                       |
| video-to-whatsapp.sh         |
| vncserver.sh                 |
| wake-alvarogonzalez.sh       |


* Carga de paquetes

Utilizo tres repositorios de paquetes:
- *melpa*: el más habitual
- *gnu*: me hizo falta para algo que no recuerdo, lo tengo actualmente deshabilitado.
- *org*: las versiones nuevas de org-mode se publican antes en este repositorio



** =use-package=

=use-package= es una utilidad para la carga y configuración de paquetes con las siguientes ventajas:
- Puede definir dependencias entre paquetes (=requires= y =after=)
- Permite la carga diferida, lo que acelera el arranque y el uso de memoria
- Agrupa la configuración de cada paquete
- Instala el paquete si no está instalado



*** Tabla de paquetes
Esta tabla contiene los paquetes que utilizo sin configuración adicional

#+tblname: mis-paquetes
#+caption: Tabla =mis-paquetes=
#+ATTR_LATEX: :environment longtable :align |l|p{10cm}|
#+NAME: mis-paquetes
|--------------------------------+----------------------------------------------------------------|
| 2048-game                      |                                                                |
| ac-js2                         |                                                                |
| adaptive-wrap                  | Indentación visual de las líneas respecto a su prefijo         |
| ag                             |                                                                |
| all-the-icons                  |                                                                |
| all-the-icons-dired            |                                                                |
| auto-highlight-symbol          | Resalte automático del símbolo bajo el cursor                  |
| bind-key                       |                                                                |
| calfw                          |                                                                |
| calfw-ical                     |                                                                |
| color-theme-sanityinc-tomorrow |                                                                |
| company                        |                                                                |
| company-auctex                 |                                                                |
| company-c-headers              |                                                                |
| company-emoji                  |                                                                |
| company-flx                    |                                                                |
| company-quickhelp              |                                                                |
| company-restclient             |                                                                |
| company-shell                  |                                                                |
| company-web                    |                                                                |
| default-text-scale             |                                                                |
| diffview                       |                                                                |
| dired-narrow                   |                                                                |
| dired-subtree                  |                                                                |
| flycheck                       |                                                                |
| flycheck-plantuml              |                                                                |
| flycheck-rust                  |                                                                |
| gift-mode                      |                                                                |
| gitignore-mode                 |                                                                |
| git-timemachine                |                                                                |
| graphviz-dot-mode              |                                                                |
| helm-ag                        |                                                                |
| helm-company                   |                                                                |
| helm-flx                       |                                                                |
| helm-gitignore                 |                                                                |
| helm-posframe                  |                                                                |
| helm-projectile                |                                                                |
| helm-swoop                     |                                                                |
| helm-tramp                     |                                                                |
| highlight-indent-guides        |                                                                |
| indent-guide                   |                                                                |
| intellij-theme                 |                                                                |
| literate-calc-mode             |                                                                |
| lorem-ipsum                    |                                                                |
| magic-latex-buffer             |                                                                |
| magit                          | Interfaz para git                                              |
| markdown-mode                  |                                                                |
| markdown-preview-mode          |                                                                |
| ob-ammonite                    |                                                                |
| ob-typescript                  |                                                                |
| ob-restclient                  |                                                                |
| ob-rust                        |                                                                |
| org-bullets                    |                                                                |
| php-mode                       |                                                                |
| plantuml-mode                  |                                                                |
| popup-imenu                    |                                                                |
| popup-switcher                 |                                                                |
| posframe                       |                                                                |
| prettier-js                    |                                                                |
| quickrun                       | Ejecuta el buffer actual con el intérprete/compilador adecuado |
| rectangle-utils                |                                                                |
| restclient                     |                                                                |
| restclient-helm                |                                                                |
| scad-mode                      |                                                                |
| scad-preview                   |                                                                |
| scala-mode                     |                                                                |
| skewer-mode                    |                                                                |
| stripe-buffer                  |                                                                |
| swiper-helm                    |                                                                |
| systemd                        |                                                                |
| transmission                   |                                                                |
| transpose-frame                |                                                                |
| treemacs-projectile            |                                                                |
| typescript-mode                |                                                                |
| use-package                    |                                                                |
| use-ttf                        | Instala la fuente por defecto en todos los sistemas            |
| vdiff                          |                                                                |
| vim-empty-lines-mode           |                                                                |
| web-beautify                   |                                                                |
| web-mode                       |                                                                |
| wgrep                          |                                                                |
| wgrep-helm                     |                                                                |
| yaml-mode                      |                                                                |
|--------------------------------+----------------------------------------------------------------|

*** Carga de paquetes sin configuración adicional
Por cada paquete sin opciones especiales, simplemente lo cargo con =use-package=, instalándolo si no está ya instalado.

El bucle =dolist= se realiza sobre los datos de la tabla =mis-paquetes=. Cada fila se recibe como una lista de columnas, así que me quedo con la primera columna y la convierto a un =symbol=. Después, construyo la llamada a =use-package= y la evaluo (como =use-package= es una macro, no puedo hacerlo directamente)
#+BEGIN_SRC emacs-lisp :var mis-paquetes=mis-paquetes
  (dolist (paquete mis-paquetes)
    (setq lista `(use-package ,(intern (car paquete)) :defer 1 :ensure t ) )
    (eval lista))
#+END_SRC




Por último, el paquete =ob-scala= es un paquete local bajado de [[https://github.com/tkf/org-mode/blob/master/lisp/ob-scala.el][https://github.com/tkf/org-mode/blob/master/lisp/ob-scala.el]]. Sirve para ejecutar código =scala= directamente desde un documento =orgmode=.

#+begin_src lisp
(add-to-list 'load-path "~/.emacs.d/mis-paquetes")
(require 'ob-scala)
#+end_src


** Paquete =simple-httpd=
Añado algunos /mime-type/ al servidor HTTP
#+BEGIN_SRC emacs-lisp
  (use-package simple-httpd
    :defer 1
    :ensure t
    :config
    (add-to-list 'httpd-mime-types '("mjs" . "text/javascript") )
    (add-to-list 'httpd-mime-types '("wasm" . "application/wasm") )
)
#+END_SRC


** rust

  #+begin_src emacs-lisp
(use-package lsp-mode
  :ensure t
  :config
)

(use-package lsp-ui
  :ensure t
)

  #+end_src


#+begin_src emacs-lisp
(use-package toml-mode
  :ensure t)

(use-package rustic
  :ensure t
  :defer 1
  :config

  (use-package cargo
    :ensure t)
  
  (defun my-enable-lsp ()
     (interactive)
     (lsp)
     (lsp-ui-mode -1))

  (add-hook 'rust-mode-hook #'my-enable-lsp)
)
#+end_src


#+begin_src emacs-lisp
(use-package flycheck-rust
  :ensure t
  :defer 1
  :config
  (with-eval-after-load 'rust-mode
    (add-hook 'flycheck-mode-hook #'flycheck-rust-setup))

)
#+end_src

#+begin_src emacs-lisp
(use-package cargo
  :ensure t
  :defer 1
  :config
    (add-hook 'rust-mode-hook #'cargo-minor-mode)
)
#+end_src





** Paquete =treemacs=
Estoy usando =treemacs= en vez de =neotree=.

#+begin_src emacs-lisp

(use-package treemacs
  :ensure t
  :defer 1
  :config

  (defun hacer-treemacs-resizable ()
    (if treemacs--width-is-locked
        (treemacs-toggle-fixed-width)))

  (defun cambiar-faces-de-treemacs ()
    ;; Cambio el tamaño por defecto del face
    (mapcar
     (lambda (face) (set-face-attribute face nil :height 0.8) )
     '(
       treemacs-directory-face                
       treemacs-directory-collapsed-face      
                                        ;treemacs-file-face                     
       treemacs-root-face                     
       treemacs-root-unreadable-face          
       treemacs-root-remote-face              
       treemacs-root-remote-unreadable-face   
       treemacs-root-remote-disconnected-face 
       treemacs-tags-face                     
       treemacs-help-title-face               
       treemacs-help-column-face              
       treemacs-git-unmodified-face
       treemacs-git-modified-face
       treemacs-git-renamed-face
       treemacs-git-ignored-face
       treemacs-git-untracked-face
       treemacs-git-added-face
       treemacs-git-conflict-face
       treemacs-term-node-face                
       treemacs-on-success-pulse-face         
       ))
    )

  
  (add-hook 'treemacs-mode-hook #'hacer-treemacs-resizable)
  (setq treemacs-silent-refresh t)
  (treemacs-git-mode -1)
  
  (cambiar-faces-de-treemacs)
  :bind
  (("<f8>" . treemacs))


  )
#+end_src

#+RESULTS:

** =ibuffer-sidebar=
   #+begin_src emacs-lisp
(use-package ibuffer-sidebar
  :defer 1
  :ensure t
  :bind
      (("C-<f8>" . ibuffer-sidebar-toggle-sidebar))
)
   #+end_src

** Paquete =swoop=
Búsqueda con previsualización. Lo configuro para que no seleccione el símbolo en en cursor, sino la seleción
#+begin_src emacs-lisp
  (use-package swoop
    :ensure t
    :defer 1
    :config

  
    (setq helm-swoop-pre-input-function
          (lambda () 
            (if (use-region-p)
                (buffer-substring-no-properties (region-beginning) (region-end))
              nil)))
    )
#+end_src


** Paquete =image+=
Image+ permite hace zoom en las imágenes
#+begin_src emacs-lisp
(use-package image+
  :ensure t
  :defer 1
  :config
    (imagex-global-sticky-mode)
    (imagex-auto-adjust-mode))
#+end_src


** Paquete =dumb-jump=
Añado las siguientes reglas para hacer búsquedas simples con =dumb-jump= en ficheros =sql= y =org=.
#+begin_src emacs-lisp
  ;; ADITIONAL DUMBJUMB RULES
  (use-package dumb-jump
    :after (helm)
    :ensure t
    :config

    (add-to-list 'dumb-jump-find-rules
                 '(:type "something" :supports ("ag" "grep" "rg" "git-grep") :language "sql"
                         :regex ": \\bJJJ\\j"))
    (add-to-list 'dumb-jump-find-rules
                 '(:type "something" :supports ("ag" "grep" "rg" "git-grep") :language "org"
                         :regex ": \\bJJJ\\j"))

    (add-to-list 'dumb-jump-language-file-exts

                 '(:language "javascript" :ext "mjs" :agtype "js" :rgtype "js")))

  (setq dumb-jump-selector 'helm)
  (add-hook 'xref-backend-functions #'dumb-jump-xref-activate)
#+end_src

** Paquete =expand-region=
#+begin_src emacs-lisp
(use-package expand-region
  :ensure t
  :defer 1
  :bind
      (( "C-e" . 'er/expand-region))
      (( "C-S-e" . 'er/contract-region))
      (( "M-<up>" . 'er/expand-region))
      (( "M-<down>" . 'er/contract-region))
)
#+end_src

** Paquete =bm=
Siempre me gustaron los /bookmarks/ dentro de un fichero de Microsoft Visual C++
#+BEGIN_SRC emacs-lisp
  (defun my/bookmark-or-edit ()
    (interactive)

    (if (string= major-mode "dired-mode")
        (progn
          (all-the-icons-dired-mode -1)
          (wdired-change-to-wdired-mode))
        (bm-next)))


    (use-package bm
      :ensure t
      :bind
      (( "<C-f2>" . bm-toggle))
      (( "<f2>" . my/bookmark-or-edit ))
      (( "<S-f2>" . bm-previous))

    )
#+END_SRC


** Paquete =multiple-cursors=

#+BEGIN_SRC emacs-lisp
  (use-package multiple-cursors
    :ensure t
    :custom (mc/always-run-for-all t)
    :bind

      (("C-S-c C-S-c" . mc/edit-lines ))
      (("C->" . mc/mark-next-like-this ))
      (("C-<" . mc/mark-previous-like-this ))
      (("C-S-<mouse-1>" . mc/add-cursor-on-click ))
      (("C-S-c C-S-v" . mc/mark-all-like-this ))
)
#+END_SRC

      


** Paquete =yasnippet=
Plantillas para introducción rápida de partes del texto. =yasnippet= interfiere con otros modos en su uso del tabulador, así que cambio su combinación.
Lo he deshabilitado porque lo uso muy rara vez, y tarda bastante en actualizarse
#+begin_src lisp
(use-package yasnippet
  :ensure t
  :config 
    (yas-global-mode 1)
    (define-key yas-minor-mode-map (kbd "<tab>") nil)
    (define-key yas-minor-mode-map (kbd "TAB") nil)
    (define-key yas-minor-mode-map (kbd "C-c TAB") 'yas-expand))

(use-package yasnippet-snippets
  :ensure t
  :after (yasnippet))
#+end_src





** Paquete =smartparents=
Este modo cierra automáticamente los paréntesis y otros bloques. Aunque lo tengo desactivado siguen apareciendo, no sé quién los pone.
#+begin_src emacs
(use-package smartparens
  :ensure t
  :config
    (smartparens-global-mode 1))
#+end_src

** Paquete =company=
Utilizo =company= como mecanismo de autocomplección. Distingo entre modos de programación y =org-mode=.

#+begin_src emacs-lisp
  (require 'company)
  (company-flx-mode +1)


  (defun my-js2-mode-hook()
    (interactive)
    (setq ac-js2-evaluate-calls t)
    (auto-highlight-symbol-mode 0)
    (js2-highlight-unused-variables-mode 1)

    (defvar my-company-backends-js2-mode
      '(
        (
         company-files
         ac-js2-company
         company-dabbrev-code
         company-capf
         )
        )
      )

    (set (make-local-variable 'company-backends) my-company-backends-js2-mode))



  (add-hook 'js2-mode-hook #'my-js2-mode-hook)





  (defvar my-company-backends-prog-mode
    '(
      (
       company-capf
       company-files
       company-dabbrev-code
       company-web-html
       company-keywords
       company-emoji
       )
      )
    )


  (defvar my-company-backends-org-mode
    '(
      (
       company-files
       company-dabbrev-code
       company-dabbrev
       company-keywords
       company-emoji
       company-capf
       )
      )
    )

  (defvar my-company-backends my-company-backends-org-mode)

  ;; set default `company-backends'
  (setq company-backends my-company-backends)

  (add-hook 'after-init-hook 'global-company-mode)

  (company-quickhelp-mode 1)

  (defun my-company-backends-org-mode-function ()
    (interactive)
    (set (make-local-variable 'company-backends) my-company-backends-org-mode))

  (add-hook 'org-mode-hook #'my-company-backends-org-mode-function)

  (defun my-company-backends-prog-mode-function ()
    (interactive)
    (set (make-local-variable 'company-backends) my-company-backends-prog-mode))


  (add-hook 'prog-mode-hook #'my-company-backends-prog-mode-function)

  (global-set-key (kbd "C-.") 'company-complete)
  (define-key company-active-map [escape] 'company-abort)
  (global-company-mode)
#+end_src


Prefiero que =dabbrev= funcione en comentarios y cadenas. Y que tenga en cuenta el /case/
#+begin_src emacs-lisp
  (setq company-dabbrev-code-everywhere t)
  (setq company-dabbrev-code-ignore-case nil)
  (setq company-dabbrev-everywhere t)
  (setq company-dabbrev-ignore-case 'keep-prefix)
  (setq company-dabbrev-downcase nil)
#+end_src

=company-box= parecía una buena idea, pero no me gusta el alto de la lista.

#+begin_src lisp
(use-package company-box
  :ensure t
  :defer 1
  :hook (company-mode . company-box-mode))
#+end_src

** Paquete =neotree=
En =neotree=, quiero ver todos los ficheros, y no me importa el ancho fijo de la ventana.
#+begin_src emacs-lisp
  (use-package neotree
    :ensure t
    :defer 1
    :config 

      ; https://github.com/jaypei/emacs-neotree/issues/149
      (defun neotree-project-root-dir-or-current-dir ()
        "Open NeoTree using the project root, using projectile, or the
      current buffer directory."
        (interactive)
        (let ((project-dir (ignore-errors (projectile-project-root)))
              (file-name (buffer-file-name))
              (neo-smart-open t))
          (if (neo-global--window-exists-p)
              (neotree-hide)
            (progn
              (neotree-show)
              (if project-dir
                  ;(neotree-dir project-dir)
                  (neotree-projectile-action))
              (if file-name
                  (neotree-find file-name))))))

      (setq neo-show-hidden-files t)
      (setq neo-window-fixed-size nil)
      (setq neo-hidden-regexp-list (quote ("\\.pyc$" "~$" "^#.*#$" "\\.elc$")))
    :bind
      (("M-<f8>" . neotree-toggle ))
)
#+end_src

* =htmlize=

#+begin_src emacs-lisp
(use-package  htmlize
  :ensure t
  :defer 1
  :config
 (setq htmlize-force-inline-images t)
 (setq htmlize-output-type 'inline-css)
)
#+end_src
  
** Paquete =org=
Con estos cambios, se tienen en cuenta los formatos de orgmode en =electric-pair-mode=
#+begin_src emacs-lisp
(use-package org
  :ensure t
  :defer 5
  :config
  
  (setq org-ellipsis "⤵⤵⤵")
  (set-face-attribute 'org-ellipsis nil :box '(:line-width 1 :color unspecified) :background 'unspecified)
  
  ;;(setq org-hide-emphasis-markers t)
  
  ;;Trying to fix indentation behaviour within code blocks.
  
  (setq org-edit-src-content-indentation 0)
  (setq org-src-fontify-natively t)
  (setq org-src-tab-acts-natively t)
  (setq org-src-preserve-indentation t)
  

  (modify-syntax-entry ?~ "(~" org-mode-syntax-table)
  (modify-syntax-entry ?= "(=" org-mode-syntax-table)
  (modify-syntax-entry ?_ "(_" org-mode-syntax-table)
  (modify-syntax-entry ?* "(*" org-mode-syntax-table)
  (modify-syntax-entry ?/ "(/" org-mode-syntax-table)

   ;; https://stackoverflow.com/questions/40110927/how-to-export-a-reference-to-a-begin-src-block-to-latex/40689439#40689439
  (setq org-latex-prefer-user-labels t)


  ;; Desde la versión 9 de orgmode, los templates rápidos no están activados por defecto si no se carga org-tempo
  (ignore-errors
    (require 'org-tempo)

    ;; si no existe org-tempo no se llega aquí, lo cual está bien porque lo siguiente solo hay que hacerlo para org-tempo
    (setq org-structure-template-alist 
          '(("n" . "notes")
            ("a" . "export ascii")
            ("c" . "center")
            ("C" . "comment")
            ("e" . "example")
            ("E" . "export")
            ("h" . "export html")
            ("l" . "export latex")
            ("q" . "quote")
            ("s" . "src")
            ("v" . "verse")))

    )
  )
#+end_src


*** Lenguajes =org-babel=
Habilito varios lenguajes que pueden ejecutarse directamente desde los bloques de =orgmode=.
#+begin_src emacs-lisp
  (if
      (file-exists-p "/usr/share/plantuml/plantuml.jar")
      (setq org-plantuml-jar-path "/usr/share/plantuml/plantuml.jar")
    (setq org-plantuml-jar-path (expand-file-name "~/.emacs.d/bin/plantuml.1.2018.11.jar")))

  (setq plantuml-jar-path org-plantuml-jar-path)

  (org-babel-do-load-languages
   'org-babel-load-languages
   '(
     (C . t )
     (dot . t)
     (plantuml . t)
     ;(scala . t)
     (maxima . t)
     (ammonite . t)
     (typescript . t)
     (rust . t)
     (shell . t)))
#+end_src

#+RESULTS:


 Además, no pido confirmación para varios lenguajes                                                                                                           
 #+begin_src emacs-lisp
 (defun my-org-confirm-babel-evaluate (lang body)
   (not (member lang '("dot" "emacs-lisp" "shell" "plantuml" "maxima"))))
 (setq org-confirm-babel-evaluate 'my-org-confirm-babel-evaluate)
#+end_src

#+RESULTS:
: my-org-confirm-babel-evaluate

   
*** Listas alfabéticas
#+begin_src emacs-lisp
(setq org-list-allow-alphabetical t)
#+end_src


*** Listados /Latex/
Utilizo el paquete =listings= de /Latex/ en vez de bloques /verbatim/.
#+begin_src emacs-lisp
(setq org-latex-listings t)
#+end_src

*** Selección con mayúsculas 
#+begin_src emacs-lisp
(setq org-support-shift-select t)
#+end_src

** Paquete =quickrun=
=Quickrun= ejecuta el buffer actual. Aumento el tiempo límite de la ejecución antes de matar el proceso.
#+begin_src emacs-lisp
(use-package quickrun
  :ensure t
  :config 
    (setq quickrun-timeout-seconds 100))
#+end_src


** Paquete =doc-view=
Para visualizar documentos desde Emacs, aumento su resolución y anchura.
#+begin_src emacs-lisp
(require 'doc-view)
(setq doc-view-continuous t)
(setq doc-view-image-width 1600)
(setq doc-view-resolution 400)
#+end_src



** Correo electrónico
Para enviar email utilizo =sendmail= (lo suelo tener configurado con un /smarthost/)
#+begin_src emacs-lisp
(setq send-mail-function (quote sendmail-send-it))
#+end_src



** =tramp=
=tramp= intenta optimizar las conexiones, enviando en línea los ficheros pequeños. Esto me da problemas en algunos sistemas, así que indico que los ficheros se copien a partir de 1 byte de tamaño:
#+begin_src emacs-lisp
(setq tramp-copy-size-limit 1)
#+end_src

A veces tengo problemas con tramp, así que tengo que activar las trazas de debug
#+begin_src lisp
(setq tramp-debug-buffer t)
(setq tramp-verbose 10)
#+end_src

En ocasiones, =tramp= no consigue conectar con un usuario que tiene =zsh= como shell. Para ello, hay que añadir lo siguiente al fichero =.zshrc= remoto:
#+begin_src sh
# EN .zshrc PARA QUE FUNCIONE tramp
if [[ "$TERM" == "dumb" ]]
then
  unsetopt zle
  unsetopt prompt_cr
  unsetopt prompt_subst
  unfunction precmd
  unfunction preexec
  PS1='$ '
fi
#+end_src



** /Backup/ de ficheros
Emacs guarda una copia de seguridad de los ficheros editados. Si no se configura, crea la copia en el mismo directorio.

Las copias de seguridad son interesantes aunque se utilice un control de versiones. Por ejemplo, se guardan versiones de ficheros del sistema y de los editados con Tramp.

Prefiero guardar todas las copias en un directorio, manteniendo varias versiones de cada fichero.

Tampoco me interesan los ficheros de /lock/.
#+begin_src emacs-lisp
(setq backup-directory-alist `(("." . "~/.saves")))
(setq backup-by-copying t)
(setq delete-old-versions t
      kept-new-versions 6
      kept-old-versions 2
      version-control t)

(setq create-lockfiles nil)

(setq auto-save-file-name-transforms
      `((".*" "~/.saves" t)))

(setq delete-by-moving-to-trash t)
#+end_src

Grabar en el propio fichero
#+begin_src emacs-lisp
(setq auto-save-visited-interval 60)
(auto-save-visited-mode 1)
#+end_src

** Latex 

#+begin_src emacs-lisp
(use-package latex
  :ensure auctex
  :config
(setq TeX-auto-save t)
(setq TeX-parse-self t)
(setq TeX-save-query nil)
(setq TeX-PDF-mode t)

; Para que funcione correctamente el resaltado de sintaxis, hay que informar a Auctex de los entornos /verbatim/ utilizados:

(setq LaTeX-verbatim-environments
      '("verbatim" "verbatim*" "listadotxt" "PantallazoTexto" "listadosql" "listadoshell" "listadojava"))


(add-to-list 'auto-mode-alist '("\\.tex\\'" . latex-mode))


; Para imenu, defino una macro de Latex que no hace nada, pero que detecto en imenu.
(add-to-list 'TeX-outline-extra '("imenu" 2))
(add-to-list 'TeX-outline-extra '("imenu1" 1))
(add-to-list 'TeX-outline-extra '("imenu2" 2))
(add-to-list 'TeX-outline-extra '("imenu3" 3))
(add-to-list 'TeX-outline-extra '("imenu4" 4))

; En Ubuntu, Evince puede sincronizarse con Emacs para saber a qué parte de código corresponde una parte del PDF y viceversa
(setq TeX-view-program-selection '((output-pdf "Okular")))
(setq TeX-source-correlate-mode t)
(add-hook 'latex-mode-hook 'TeX-source-correlate-mode)

(setq TeX-source-correlate-start-server t)



; Modifico el comando Latex para incluir =-shell-escape=, de forma que Latex pueda arrancar programas de ayuda (por ejemplo, *Inkscape* para convertir SVG a PDF)

(setq LaTeX-command-style
   (quote (("" "%(PDF)%(latex) %(file-line-error) -shell-escape %(extraopts) %S%(PDFout)"))))


; Se pueden previsualizar los entornos =tikzpicture= y =tabular= directamente en el buffer de Emacs ([[https://www.gnu.org/software/auctex/manual/preview-latex.html][https://www.gnu.org/software/auctex/manual/preview-latex.html]])

(eval-after-load "preview"
  '(add-to-list 'preview-default-preamble "\\PreviewEnvironment{tikzpicture}" t) )
(eval-after-load "preview"
  '(add-to-list 'preview-default-preamble "\\PreviewEnvironment{tabular}" t) )
(eval-after-load "preview"
  '(add-to-list 'preview-default-preamble "\\PreviewEnvironment{homeworkProblem}" t) )

; Añadir XeLatex
(eval-after-load "tex"
  '(add-to-list 'TeX-command-list
     		'("XeLaTeX" "xelatex -interaction=nonstopmode %s"
		  TeX-run-command t t :help "Run xelatex") t))

;; En =Termux= se necesita especificar la camino a la /shell/ para ejecutar comandos de /Latex/
(if
      (file-exists-p "/data/data/com.termux/files/usr/bin/sh")
      (setq TeX-shell  "/data/data/com.termux/files/usr/bin/sh"))
)
#+END_SRC

#+RESULTS:
: t





** Paquete =which-key=

Ayuda interactiva de teclado. En el popup-type por defecto entra en conflicto con treemacs.
#+begin_src emacs-lisp
(use-package which-key
  :ensure t
  :config
    (which-key-mode t)
    (setq which-key-idle-delay 3.0)
)

(use-package which-key-posframe
  :ensure t
  :config
  (which-key-posframe-mode)

)
#+end_src



** Paquete =auto-highlight-symbol=
Resaltar el símbolo bajo el cursor de forma dinámica. Antes lo resaltaba en todo el buffer, para que se pueda navegar por todas las ocurrencias del fichero, pero ralentizaba bastante. Ahora uso =smartscan=.
#+begin_src emacs-lisp
  (use-package auto-highlight-symbol
    :ensure t
    :config
      (setq ahs-idle-interval 3.0)
      (setq ahs-default-range 'ahs-range-display))
#+end_src



** =transpose-frame=

   #+begin_src emacs-lisp

(use-package transpose-frame 
  :ensure t
  :defer 1
  :bind
      (("<f5>" . transpose-frame))
)

   #+end_src

** Paquete =helm=
=helm= es un sistema para seleccionar una opción entre varias posibilidades, que se puede usar para casi todo
- Buscar un comando
- Cambiar de buffer
- Navegar por la historia del portapapeles
- Visualizar las ocurrencias de un patrón en un buffer
- ... y más

#+begin_src emacs-lisp

(when nil
  (use-package helm-icons
    :ensure t
    :defer 1
    :config
    (helm-icons-enable)
    ))


;; HELM
(use-package helm
  :ensure t
  :defer 1
  :config
  (setq helm-split-window-inside-p t)
  (setq helm-display-header-line nil)
  (setq helm-autoresize-max-height 40)
  (setq helm-autoresize-min-height 30)
  (helm-autoresize-mode 1)
  (helm-mode 1)
  (helm-flx-mode +1)
  (setq helm-display-buffer-reuse-frame t)
  (setq helm-use-undecorated-frame-option t)
  ;(setq helm-display-function 'helm-display-buffer-in-own-frame)

  ;; PROBLEMAS DE REDISPLAY: hasta que no doy una tecla no se muestra nada
  (defun my/try-to-redisplay-helm ()
    (redisplay t))
  (add-hook 'helm-minibuffer-set-up-hook 'my/try-to-redisplay-helm)


  
  :after (tramp) ;; PARA EVITAR EL ERROR Symbols's value as variable is void: tramp-methods
  :bind
  (("M-x"  . helm-M-x))
  (("<menu>"  . helm-M-x))
  (("C-x C-f"  . helm-find-files))
  (("<f6>"  . helm-mini))
  (("M-y"  . helm-show-kill-ring))
  (("C-x r b"  . helm-filtered-bookmarks))

  )
#+end_src


Intento de colocar la ventana de helm siempre abajo
#+begin_src emacs-lisp
    ;;https://www.reddit.com/r/emacs/comments/345vtl/make_helm_window_at_the_bottom_without_using_any/
    ;;HELM SIEMPRE ABAJO CON ANCHO COMPLETO
    (add-to-list 'display-buffer-alist
                    `(,(rx bos "*helm" (* not-newline) "*" eos)
                         (display-buffer-in-side-window)
                         (inhibit-same-window . t)
                         (window-height . 0.3)))
#+end_src

=helm-posframe= muestra las ventanas de helm en una frame nueva, pero no funciona del todo bien (tabulador)
#+begin_src lisp
(use-package helm-posframe
  :ensure t
  :defer t
  :config
  (setq helm-posframe-parameters '(:internal-border-width 40 :internal-border-color "pink"))
  (setq helm-posframe-border-width 2)
  (setq helm-posframe-poshandler 'posframe-poshandler-frame-top-center)
  (helm-posframe-enable)

  :after (helm)
  )
#+end_src

=helm-preview=
#+begin_src lisp
(use-package helm-file-preview
  :ensure t
  :defer t
  :after (helm)
  :config
  (helm-file-preview-mode 1)
  (setq helm-file-preview-only-when-line-numbers nil)
  (setq helm-file-preview-preview-only t))
#+end_src


*** /Child frame/
=helm= se muestra en una nueva ventana. Esta ventana puede estar en una nueva /child frame/ para no cambiar la disposición de la /frame/ original. Estas opción es bastante lenta en algunos sistemas de ventanas.
#+begin_src lisp
(use-package helm
  :config
  (setq helm-display-function 'helm-display-buffer-in-own-frame
        helm-display-buffer-width 120)
  
  ;; helm-popup-frame debería tener el foco

  (setq swiper-helm-display-function 'helm-posframe-display) ;'helm-display-function)
)
#+end_src


~helm-swoop~ no funciona bien con =helm-posframe=, al seleccionar una ocurrencia no salta a ella.
#+begin_src emacs-lisp
(use-package helm-swoop
  :ensure t
  :config
  (setq helm-display-buffer-width 140)
  (setq helm-display-buffer-height 20)
  (setq helm-swoop-split-window-function 'helm-default-display-buffer)
  :after (helm)
  :bind
      (("C-f" . helm-swoop))
      (("C-S-f" . helm-multi-swoop-all))

)

#+end_src







** =projectile=
=projectile= necesita conocer su tecla de prefijo (utilizo la tradicional).
#+begin_src emacs-lisp
(use-package projectile
  :ensure t
  :config
    (define-key projectile-mode-map (kbd "C-c p") 'projectile-command-map)
    (setq projectile-switch-project-action 'projectile-dired)
    (setq projectile-indexing-method 'alien)
    (setq projectile-enable-caching t)

    (projectile-mode 1)
    (setq projectile-switch-project-action 'projectile-dired))

(use-package helm-projectile
  :ensure t
  :defer 1
  :config 
    (helm-projectile-on)
    (setq projectile-completion-system 'helm)
  :after (helm)
)

#+end_src

#+RESULTS:
: t


*** Paquete =ox-epub=

#+begin_src emacs-lisp
(use-package ox-epub
  :ensure t
  :defer 1
)


#+end_src

#+RESULTS:
: [nil 0 1 0 nil require (ox-epub nil t) idle 0]

*** Paquete =org-re-reveal=
#+begin_src emacs-lisp
(use-package org-re-reveal
  :ensure t
  :config
)
#+end_src

#+RESULTS:








* Edición
** Acentos en KDE
Desde que uso Plasma no funcionan bien los acentos. Pero ahora sí que funcionan
#+begin_src lisp
(require 'iso-transl)
#+end_src
** Expresiones regulares para find/replace
#+begin_src emacs-lisp

(use-package visual-regexp
  :defer 1
  :ensure t
  :bind
  (( "C-M-%" . vr/query-replace))
  )

#+end_src

** Tabuladores /vs/ espacios
No utilizo tabuladores en las indentaciones. 
#+begin_src emacs-lisp
(setq-default indent-tabs-mode nil)
(setq tab-width 2)
#+end_src

** Comportamiento de la selección
Al comenzar a escribir con una selección, se borra lo seleccionado. 
#+begin_src emacs-lisp
(delete-selection-mode 1)
#+end_src

Al copiar la  selección, mantener la selección 
#+begin_src emacs-lisp
(defadvice kill-ring-save (after keep-transient-mark-active ())
  "Override the deactivation of the mark."
  (setq deactivate-mark nil))
(ad-activate 'kill-ring-save)
#+end_src


** Línea nueva al final de fichero
Los ficheros deben tener una línea nueva al final. Además, indicar el fin de fichero como en =vim=.
#+begin_src emacs-lisp
(setq indicate-empty-lines t require-final-newline t)
#+end_src


** Historia del portapapeles
Una de las ventajas de Emacs es su /kill ring/, donde se guarda la historia del portapapeles. Con esta opción, añado a esta historia el portapapeles del sistema. Descubierto en [[https://writequit.org/org/settings.html#sec-1-33][https://writequit.org/org/settings.html#sec-1-33]]
#+begin_src emacs-lisp
(setq save-interprogram-paste-before-kill t)
#+end_src

** Recarga de ficheros modificados
Encuentro más conveniente que los ficheros se recarguen si un programa externo los modifica, sin preguntas.

#+begin_src emacs-lisp
(use-package autorevert
  :ensure nil
  :config
  (global-auto-revert-mode 1)
  (setq global-auto-revert-non-file-buffers t)
  (setq auto-revert-verbose nil))
#+end_src

** Comandos que se consideran /avanzados/
Emacs tiene algunos compandos considerados confusos deshabilitados. Hay opciones útiles que prefiero que estén activadas por defecto.
#+begin_src emacs-lisp
(put 'narrow-to-region 'disabled nil)
(put 'upcase-region 'disabled nil)
(put 'downcase-region 'disabled nil)
#+end_src



** Javascript

   #+begin_src emacs-lisp
(add-to-list 'auto-mode-alist '("\\.mjs\\'" . js2-mode))
   #+end_src


* Navegación

Modo de ficheros recientes
#+begin_src emacs-lisp
(recentf-mode 1)
(setq recentf-max-saved-items 2000)
#+end_src

Guardar el fichero de bookmarks cada vez que se modifiquen
#+begin_src emacs-lisp
(setq bookmark-save-flag 1)
#+end_src



Algunas ventanas tienen menor /importancia/ que otras, ya que tienden a ser temporales (por ejemplo, las ventanas de ayuda). Con =popwin=, estas ventanas ocupan menos espacio en pantalla y desaparecen con =C-g=. Actualmente lo he quitado.
#+begin_src lisp
(use-package popwin
  :ensure t
  :defer 1
  :config
    (popwin-mode 1))
#+end_src


Agrupo los buffers por proyecto de =projectile=
#+begin_src emacs-lisp
  (use-package ibuffer-projectile
    :ensure t
    :defer 1
    :config
      (add-hook 'ibuffer-hook #'ibuffer-projectile-set-filter-groups)
      (add-hook 'ibuffer-sidebar-mode-hook #'ibuffer-projectile-set-filter-groups))

(add-hook 'ibuffer-mode-hook (lambda () (ibuffer-auto-mode 1)))
#+end_src


Retroceder en la historia de disposición de ventanas y búferes
#+begin_src emacs-lisp
(use-package winner
  :ensure t
  :defer 1 
  :config
    (winner-mode 1))
#+end_src

* Ratón en =xterm=
El ratón también puede utilizarse en un =xterm=
#+begin_src emacs-lisp
(xterm-mouse-mode)
#+end_src


* Visualización

** Previsualización de ficheros en Helm
#+begin_src lisp
(use-package helm-file-preview
  :ensure t
  :defer 1
  :config
  (setq helm-file-preview-only-when-line-numbers nil)
  (helm-file-preview-mode 1))
#+end_src


** Líneas muy largas
Avoid performance issues in files with very long lines.
#+begin_src emacs-lisp
(unless (version<= emacs-version "27")
  (global-so-long-mode 1))
#+end_src

** Pestañas
#+begin_src emacs-lisp


(use-package centaur-tabs
  :ensure t
  :defer 1
  :init
  (setq centaur-tabs-enable-key-bindings t)
  :config

  (defun my-centaur-tabs-font-sizes (&optional height)
    (interactive
     (list 
      (read-number "height: " 0.8)))

    (mapcar
     (lambda (face) (set-face-attribute face nil :height (or height 0.8)) )
     '(
       centaur-tabs-active-bar-face
       centaur-tabs-default
       centaur-tabs-unselected
       centaur-tabs-selected
       centaur-tabs-unselected-modified
       centaur-tabs-selected-modified
       centaur-tabs-close-unselected
       centaur-tabs-close-selected
       centaur-tabs-close-mouse-face
       centaur-tabs-modified-marker-selected
       centaur-tabs-modified-marker-unselected
       ))

    (setq centaur-tabs-height 10)
    (setq centaur-tabs-bar-height (+ 8 centaur-tabs-height)))



  (setq centaur-tabs-set-icons t)
  (setq centaur-tabs-style "alternate")
  (setq centaur-tabs-set-modified-marker t)
  (setq centaur-tabs-gray-out-icons 'buffer)
  (setq centaur-tabs-set-bar 'over)
  (setq centaur-tabs-cycle-scope 'tabs)
  (setq centaur-tabs-show-navigation-buttons t)

  (defun my-centaur-tabs-hide-tab (x)
    "Incluir magit entre los visibles."
    (let ((name (format "%s" x)))
      (or
       ;; Current window is not dedicated window.
       (window-dedicated-p (selected-window))

       ;; Buffer name not match below blacklist.
       (string-prefix-p "*epc" name)
       (string-prefix-p "*helm" name)
       (string-prefix-p "*Compile-Log*" name)
       (string-prefix-p "*lsp" name)
       (string-prefix-p "*company" name)
       (string-prefix-p "*Flycheck" name)
       (string-prefix-p "*tramp" name)
       (string-prefix-p " *Mini" name)
       (string-prefix-p "*help" name)
       (string-prefix-p "*Help" name)
       (string-prefix-p "*Helm" name)
       (string-prefix-p "*diff-hl" name)

       )))

  (setq centaur-tabs-hide-tabs-hooks '(reb-mode-hook completion-list-mode-hook))
  (setq centaur-tabs-hide-tab-function 'my-centaur-tabs-hide-tab)


  (my-centaur-tabs-font-sizes)

  (centaur-tabs-mode t)
  (centaur-tabs-group-by-projectile-project)


)
#+end_src




** Resaltar términos buscados
=isearch= resalta las coincidencias con la búsqueda, pero se quitan al acabar de buscar. Con esto, las coincidencias quedan resaltadas hasta la siguiente búsqueda o hasta ejecutar =lazy-highlight-cleanup=
#+begin_src emacs-lisp
(setq lazy-highlight-cleanup nil)
(setq lazy-highlight-max-at-a-time nil)
(setq lazy-highlight-initial-delay 0)
#+end_src

** Marcar las ocurrencias de la selección
#+begin_src emacs-lisp
(use-package region-occurrences-highlighter 
  :ensure t
  :defer 1
  :config
  (add-hook 'prog-mode-hook #'region-occurrences-highlighter-mode)
  (add-hook 'org-mode-hook #'region-occurrences-highlighter-mode)

  (add-hook 'text-mode-hook #'region-occurrences-highlighter-mode)

  (define-key region-occurrences-highlighter-nav-mode-map "\M-n" 'region-occurrences-highlighter-next)
  (define-key region-occurrences-highlighter-nav-mode-map "\M-p" 'region-occurrences-highlighter-prev))
#+end_src

** Saltar rápidamente entre símbolos
Utilizo =smartscan= para localizar ocurrencias de símbolos.
Las teclas rápidas son M-n y M-p, que entran en conflicto con las de region-occurrences-highlighter-nav-mode-map. Cargo este paquete después para que en minor-mode-map-alist aparezca antes el keymap de region-occurrences.
#+begin_src emacs-lisp
(use-package smartscan
  :ensure t
  :defer 1
  :config
    (setq smartscan-symbol-selector "symbol")
    (global-smartscan-mode 1))
#+end_src


** Cambiar el tamaño de fuente de todo /emacs/ (no solo el buffer actual)
#+begin_src emacs-lisp
(default-text-scale-mode 1)
(setq frame-resize-pixelwise t)
#+end_src


** Marcar la línea actual. 
A veces lo deshabilito porque no funciona bien con /overlays/
#+begin_src lisp
(global-hl-line-mode 1)
#+end_src

** Respuestas de confirmación más cortas
#+begin_src emacs-lisp
(fset 'yes-or-no-p 'y-or-n-p)
#+end_src

** Desactivar la campana (/bell/)
Tanto la señal auditiva como la visual
#+begin_src emacs-lisp
(setq visible-bell 1)
(setq ring-bell-function 'ignore)
#+end_src


** /flycheck/ 
Marca errores en el fichero
#+begin_src emacs-lisp
(add-hook 'after-init-hook #'global-flycheck-mode)
;(setq flycheck-shellcheck-follow-sources nil)

(use-package flycheck-inline
  :ensure t
  :defer 1
  :config
    (with-eval-after-load 'flycheck
      (add-hook 'flycheck-mode-hook #'turn-on-flycheck-inline))
)
#+end_src

** /scroll/
El /scroll/  de /emacs/ es de media en media pantalla, heredado de los terminales modo texto que costaba refrescar. Con los ordenadores actuales, mejor un /scroll/ suave
#+begin_src emacs-lisp
(use-package good-scroll
  :ensure t
  :config
  (good-scroll-mode 1))


(setq scroll-margin 0
     scroll-step 1
     scroll-conservatively 10000
     scroll-preserve-screen-position 1)

(setq jit-lock-defer-time 0)
(setq fast-but-imprecise-scrolling t)
#+end_src

#+RESULTS:
: t

#+begin_src emacs-lisp
(setq scroll-error-top-bottom t)
#+end_src

** Velocidad de la rueda de ratón

Scroll con teclas de avance de página hasta el extremo del fichero. Sin esta opción, /Emacs/ no avanza hasta la primera línea si al dar a =RePag= no quedan páginas por retroceder.
   
[[https://stackoverflow.com/questions/445873/how-can-i-make-emacs-mouse-scrolling-slower-and-smoother]]
   #+begin_src emacs-lisp
(setq mouse-wheel-progressive-speed nil)
(setq mouse-wheel-scroll-amount '(1 ((shift) . 1) ((control) . nil))) 
   #+end_src


** Barras de desplazamiento, de herramientas y menú
La barra de menú y la de herramientas es de lo primero que se quita al personalizar /emacs/, lo mismo que esa pantalla de inicio.
#+begin_src emacs-lisp
(setq inhibit-startup-message t)
(ignore-errors (menu-bar-mode -1))
(ignore-errors (tool-bar-mode -1))
(ignore-errors (scroll-bar-mode -1))
#+end_src

** Ancho de la página de =man=
#+begin_src emacs-lisp
(setenv "MANWIDTH" "80")
#+end_src

** Mostrar paréntesis asociados
#+begin_src emacs-lisp
(show-paren-mode)
#+end_src

** Servidor emacs
Arranco el servidor para utilizar /emacsclient/
#+begin_src emacs-lisp
  (use-package server
    :config
    (setenv "EDITOR" "emacsclient")
    (unless (server-running-p)
      (server-start))
    )
#+end_src




** =minimap=
#+begin_src emacs-lisp
(use-package minimap
  :ensure t
  :defer 1
  :config



 (setq minimap-hide-fringes t)
 (setq minimap-major-modes '(org-mode tex-mode prog-mode))
 (setq minimap-window-location 'right)
 (minimap-mode -1)

)
#+end_src


** =dired=
Coloco primero los directorios, y hago que la tecla =Tab= abra un subárbol

#+begin_src emacs-lisp
(use-package dired-x)

(use-package dired-subtree 
  :ensure t
  :defer 1
  :config

  (set-face-attribute 'dired-subtree-depth-1-face nil :background 'unspecified)
  (set-face-attribute 'dired-subtree-depth-2-face nil :background 'unspecified)
  (set-face-attribute 'dired-subtree-depth-3-face nil :background 'unspecified)
  (set-face-attribute 'dired-subtree-depth-4-face nil :background 'unspecified)
  (set-face-attribute 'dired-subtree-depth-5-face nil :background 'unspecified)
  (set-face-attribute 'dired-subtree-depth-6-face nil :background 'unspecified)

  (setq dired-listing-switches "-aBhl --group-directories-first")


  (defun my/dired-hook ()
    (interactive)
    (all-the-icons-dired-mode 1)
    (dired-hide-details-mode 1)
    (auto-revert-mode 1)
    (diff-hl-dired-mode 1)
    (setq  indent-line-function 'dired-subtree-toggle))

  (add-hook 'dired-mode-hook 'my/dired-hook)

  )


#+end_src

#+RESULTS:
: t


* Atajos de teclado


Si empiezo una búsqueda con =C-s= o =C-r=, se empieza buscando la selección
#+begin_src emacs-lisp
;; https://www.reddit.com/r/emacs/comments/b7yjje/isearch_region_search/
(defun stribb/isearch-region (&optional not-regexp no-recursive-edit)
  "If a region is active, make this the isearch default search pattern."
  (interactive "P\np")
  (when (use-region-p)
    (let ((search (buffer-substring-no-properties
                   (region-beginning)
                   (region-end))))
      ;;;(message "stribb/ir: %s %d %d" search (region-beginning) (region-end))
      (setq deactivate-mark t)
      (isearch-yank-string search))))

(advice-add 'isearch-forward-regexp :after 'stribb/isearch-region)
(advice-add 'isearch-forward :after 'stribb/isearch-region)

(advice-add 'isearch-backward-regexp :after 'stribb/isearch-region)
(advice-add 'isearch-backward :after 'stribb/isearch-region)
#+end_src

#+RESULTS:

Cuando quiero cerrar un buffer, prefiero que no pregunte.
#+begin_src emacs-lisp
(defun kill-this-buffer-dont-ask ()
  (interactive)
  (kill-buffer (current-buffer)))
(global-set-key (kbd "C-x k") 'kill-this-buffer-dont-ask)
#+end_src


En una búsqueda incremental, utilizo los cursores para ir a otras búsquedas anteriores o para navegar entre las ocurrencias en el fichero
#+begin_src emacs-lisp
  ;; set arrow keys in isearch. left/right is backward/forward, up/down is history. press Return to exit
  (define-key isearch-mode-map (kbd "<up>") 'isearch-ring-retreat )
  (define-key isearch-mode-map (kbd "<down>") 'isearch-ring-advance )

  (define-key isearch-mode-map (kbd "<left>") 'isearch-repeat-backward)
  (define-key isearch-mode-map (kbd "<right>") 'isearch-repeat-forward)

  (define-key minibuffer-local-isearch-map (kbd "<left>") 'isearch-reverse-exit-minibuffer)
  (define-key minibuffer-local-isearch-map (kbd "<right>") 'isearch-forward-exit-minibuffer)


#+end_src


A veces es fácil perderse entre comandos a medio introducir y ventanas popup. Me gusta que la tecla escape cancele cualquier acción. Con el siguiente código hago que se cancelen incluso más acciones que con =C-g=.
#+begin_src emacs-lisp
;;(define-key global-map [escape] 'keyboard-escape-quit)
;; (define-key key-translation-map (kbd "ESC") (kbd "C-g")) // PROBLEMAS CON EL TERMINAL
(defun super-escape()
  (interactive)
  (keyboard-escape-quit)
  (keyboard-quit)
  (company-abort)
  (abort-recursive-edit)
  (setq quit-flag t))
(define-key global-map [escape] 'super-escape)
(define-key minibuffer-local-map [escape] 'super-escape)
(define-key company-active-map [escape] 'company-abort)
#+end_src



Algunas teclas definidas a nivel global son sobreescritas por algunos modos (por ejemplo, prefiero que =C-Z= sea "deshacer"). Para poder definir teclas con prioridad sobre los demás modos defino un modo con mis atajos.
#+begin_src emacs-lisp

  ;; MIS TECLAS
  (defvar mis-teclas-minor-mode-map
    (let ((map (make-sparse-keymap)))

      (define-key map (kbd "C-z") 'undo )
      (define-key map (kbd "C-x C-d") 'dired)
      (define-key map (kbd "C-x d") 'dired-other-frame)
      (define-key map (kbd "C-x C-b") 'ibuffer)
      (define-key map (kbd "C-x b") 'ibuffer)
      ;(define-key map (kbd "C-f") 'swiper-helm)
      (define-key map (kbd "C-<f5>") 'reveal-y-pdf)
      ;define-key map (kbd "<backtab>") 'psw-switch-buffer)
      (define-key map (kbd "M-I") 'popup-imenu)
      (define-key map (kbd "<f7>") 'imenu-list-smart-toggle)

      (define-key map (kbd "M-S-<up>") 'enlarge-window)
      (define-key map (kbd "M-S-<down>") 'shrink-window)
      (define-key map (kbd "M-S-<left>") 'shrink-window-horizontally)
      (define-key map (kbd "M-S-<right>") 'enlarge-window-horizontally)
      (define-key map (kbd "C-x M-x") 'execute-extended-command)
      (define-key map (kbd "C-S-l") 'toggle-truncate-lines)
      map)
    "mis-teclas-minor-mode keymap")


  (define-minor-mode mis-teclas-minor-mode
    "A minor mode so that my key settings override annoying major modes."
    :init-value t
    :lighter "mis-teclas")

  (mis-teclas-minor-mode 1)

#+end_src





* Utilidades

** =sudo-edit=
   #+begin_src emacs-lisp
(use-package sudo-edit
  :ensure t
  :defer 1
  :config
  (defun abrir-como-root()
    (interactive)
    (sudo-edit))
)

   #+end_src

   #+RESULTS:
   : t

** =helpful=

#+begin_src emacs-lisp
(use-package helpful
  :ensure t
  :bind
  ([remap describe-symbol]      . helpful-symbol)
  ([remap describe-callable]    . helpful-callable)
  ([remap describe-function]    . helpful-function)
  ([remap describe-variable]    . helpful-variable)
  ([remap describe-key]         . helpful-key)
  ([remap view-emacs-debugging] . helpful-at-point)

)
#+end_src


** Deshabilitar posframe en terminal
#+begin_src emacs-lisp
(defun my/disable-posframe ()
  (interactive)
  (helm-posframe-disable)
  (which-key-posframe-mode -1))

(defun my/enable-posframe ()
  (interactive)
  (helm-posframe-enable)
  (which-key-posframe-mode 1))

(defun my/disable-or-enable-posframe ()
  "Enable or disable integrations with posframe."
  (interactive)
  (message "EN TIMER")
  (unless (posframe-workable-p) (my/disable-posframe))
  (when (posframe-workable-p) (my/enable-posframe)))


#+end_src


** =magit=

   #+begin_src emacs-lisp
(use-package magit
  :ensure t
  :defer 1
  :bind
      (("<f9>" . magit-status)
       ("C-x g" . magit-status))

  :config
  (remove-hook 'magit-status-sections-hook 'magit-insert-tags-header)
  (remove-hook 'magit-status-sections-hook 'magit-insert-unpushed-to-pushremote)
  (remove-hook 'magit-status-sections-hook 'magit-insert-unpulled-from-pushremote)
  (remove-hook 'magit-status-sections-hook 'magit-insert-unpulled-from-upstream)
  (remove-hook 'magit-status-sections-hook 'magit-insert-unpushed-to-upstream-or-recent)
)
   #+end_src

** =vdiff-magit=
#+begin_src emacs-lisp
  (use-package vdiff-magit
    :ensure t
    :defer 1
    :config

    (define-key magit-mode-map "e" 'vdiff-magit-dwim)
    (define-key magit-mode-map "E" 'vdiff-magit)
    (transient-suffix-put 'magit-dispatch "e" :description "vdiff (dwim)")
    (transient-suffix-put 'magit-dispatch "e" :command 'vdiff-magit-dwim)
    (transient-suffix-put 'magit-dispatch "E" :description "vdiff")
    (transient-suffix-put 'magit-dispatch "E" :command 'vdiff-magit)

    ;; This flag will default to using ediff for merges.
    (setq vdiff-magit-use-ediff-for-merges nil)

    ;; Whether vdiff-magit-dwim runs show variants on hunks.  If non-nil,
    ;; vdiff-magit-show-staged or vdiff-magit-show-unstaged are called based on what
    ;; section the hunk is in.  Otherwise, vdiff-magit-dwim runs vdiff-magit-stage
    ;; when point is on an uncommitted hunk.
    ;; (setq vdiff-magit-dwim-show-on-hunks nil)

    ;; Whether vdiff-magit-show-stash shows the state of the index.
    ;; (setq vdiff-magit-show-stash-with-index t)

    ;; Only use two buffers (working file and index) for vdiff-magit-stage
    (setq vdiff-magit-stage-is-2way nil)
    )
#+end_src

#+RESULTS:
: t


** Convertir un fichero =gift= en un examen =pdf=
Tengo un proyecto personal en [[https://github.com/alvarogonzalezsotillo/grading-questionnaire]] que [[https://alvarogonzalezsotillo.github.io/blog/plantilla-latex-para-examenes/][convierte un fichero =gift= en un =pdf=]] para exámenes. Esta función me facilita la conversión.
   #+begin_src emacs-lisp
     (defun gifttolatex (titulo porcentajetest parametros)
       "ejecuta giftolatex en el buffer actual."
       (interactive
        (list 
         (read-string "Título: " (buffer-name))
         (read-number "Porcentaje del test: " 60)
         (read-string "Otros parámetros:" "-k"))
        )
  
       (let
           ((comando (format "sh -c 'gifttolatex %s -t \"%s\" -q \"%s\" \"%s\"'" parametros titulo porcentajetest buffer-file-name))
            )
         (shell-command comando)
         )
       )

       (defalias 'trim-string 'string-trim) 
   #+end_src

   #+RESULTS:
   : gifttolatex


** Inicio de una selección rectangular usando el ratón (lo uso poco, prefiero =C-x spc=)
#+begin_src emacs-lisp
;; https://emacs.stackexchange.com/questions/7244/enable-emacs-column-selection-using-mouse
(defun mouse-start-rectangle (start-event)
  (interactive "e")
  (deactivate-mark)
  (mouse-set-point start-event)
  (rectangle-mark-mode +1)
  (let ((drag-event))
    (track-mouse
      (while (progn
               (setq drag-event (read-event))
               (mouse-movement-p drag-event))
        (mouse-set-point drag-event)))))

(global-set-key (kbd "S-<down-mouse-1>") #'mouse-start-rectangle)

#+end_src

** Arrancar el servidor http de emacs en el directorio actual
#+begin_src emacs-lisp
  (defun servidor-httpd-aqui (directory host port)
    "Abre un servidor http en un directorio."
    (interactive   (list
    (read-directory-name "Root directory: " default-directory nil t)
    (read-string "Host: " "127.0.0.1" )
    (read-number "Port: " 8080)))

    (setq httpd-root directory)
    (setq httpd-host host)
    (setq httpd-port port)
    (httpd-start)
    (browse-url (concat "http://localhost:" (number-to-string port) "/")))
#+end_src


** /Transmission/
Cuando hay que añadir muchos torrents similares, es muy útil hacerlo desde un buffer de emacs.
#+begin_src emacs-lisp
;; CONECTAR A TRANSMISSION
(defun conectar-a-transmission ()
  (interactive)

  (setq transmission-host (read-string "Transmission host: " "192.168.1.254" ))
  (setq transmission-user (read-string "Transmission user: " "transmission"))
  (setq transmission-pass (read-passwd "Transmission password: "))

  (message "Conectando a %s@%s" transmission-user transmission-host)
  
  (setq transmission-rpc-auth (list ':username transmission-user ':password transmission-pass))

  (transmission))

#+end_src

** Generar /reveal/ y PDF
Generalmente utilizo un fichero orgmode para hacer transparencias y materiales para clase, y quiero generar a la vez las transparencias, la versión HTML y el PDF.
#+begin_src emacs-lisp
(defun reveal-y-pdf ()
  "Crea transparencias de reveal y hace el pdf a la vez."
  (interactive)
  (ignore-errors
    (org-html-export-to-html)
    (let* (
           (filename (buffer-file-name))
           (html-filename (concat (file-name-sans-extension filename) ".html"))
           (html-wp-filename (concat (file-name-sans-extension filename) ".wp.html")) )
      (message "Copiando fichero: %s -> %s" html-filename html-wp-filename)
      (copy-file html-filename html-wp-filename t) ))

  (ignore-errors
    (org-re-reveal-export-to-html)
    (let* (
           (filename (buffer-file-name))
           (html-filename (concat (file-name-sans-extension filename) ".html"))
           (html-reveal-filename (concat (file-name-sans-extension filename) ".reveal.html")) )
      (message "renombrando fichero: %s -> %s" html-filename html-reveal-filename)
      (rename-file html-filename html-reveal-filename t)))

  (ignore-errors
    (org-epub-export-to-epub))

  (ignore-errors
    (org-latex-export-to-pdf)
    (let* (
           (filename (buffer-file-name))
           (tex-filename (concat (file-name-sans-extension filename) ".tex")))

      
      (message "Borrando fichero: %s" tex-filename)
      (delete-file tex-filename) ) ))





#+end_src

#+RESULTS:

** Función para decodificar una URL
#+begin_src emacs-lisp
(defun url-decode-region (start end)
  "Replace a region with the same contents, only URL decoded."
  (interactive "r")
  (let ((text (url-unhex-string (buffer-substring start end))))
    (delete-region start end)
    (insert text)))

#+end_src

** Horario
Este es mi horario lectivo (sin incluir guardias y otras horas que no son de docencia directa a alumnos)
#+begin_src emacs-lisp
(defun horario()
  (interactive)
  (cfw:open-ical-calendar "https://calendar.google.com/calendar/ical/am7ho5i6q1t325t195e5jfikr4%40group.calendar.google.com/private-0d7934327f65cd42fbe73d7161d521c8/basic.ics"))


#+end_src

#+RESULTS:
: horario

** Proxy de educamadrid
Durante una temporada, en los colegios de la Comunidad de Madrid era obligatorio el uso de un proxy.
#+begin_src lisp
(defun quitar-proxy()
  (interactive)
  (setq url-proxy-services '()))

(defun proxy-educamadrid()
  (interactive)
  (setq url-proxy-services
        '(("no_proxy" . "^\\(localhost\\|10\\.*|192\\.*\\)")
          ("http" . "213.0.88.85:8080")
          ("https" . "213.0.88.85:8080"))))

#+end_src

** Inserta la imagen del portapapeles en orgmode.
No lo uso mucho, quizás si cambio el nombre autogenerado sea más útil.
#+begin_src emacs-lisp
(defun org-insert-clipboard-image()
  "Save the image in the clipboard  into a time stamped unique-named file in the same directory as the org-buffer and insert a link to this file."
  (interactive)
  ; (setq tilde-buffer-filename (replace-regexp-in-string "/" "\\" (buffer-file-name) t t))
  (setq filename
        (concat
         (make-temp-name
          (concat buffer-file-name
                  "_"
                  (format-time-string "%Y%m%d_%H%M%S_")) ) ".png"))
  ;; Linux: ImageMagick:
  ;(call-process "/bin/bash" nil (list filename "kk") nil "-c" "xclip -selection clipboard -t image/png -o")
  (call-process "xclip" nil (list :file filename) nil "-selection"  "clipboard" "-t" "image/png" "-o")
  (insert (concat "[[file:" filename "]]"))
  (org-display-inline-images))
#+end_src

** Compilar todo ELPA
   #+begin_src emacs-lisp
(defun native-compile-all ()
  (interactive)
  (native-compile-async "~/.emacs.d/elpa" 'recursively))
   #+end_src


** Eliminar el resto de buffers y ventanas
#+begin_src emacs-lisp
(defun kill-other-buffers ()
  "Kill all otherbuffers."
  (interactive)
  (mapc
   'kill-buffer
   (delq (current-buffer)
         (remove-if-not
          '(lambda (x)
             (or (buffer-file-name x)
                 (eq 'dired-mode (buffer-local-value 'major-mode  x))))
          (buffer-list)))))

#+end_src

** Convertir la selección en un bloque de código de =orgmode=
#+begin_src emacs-lisp
(defun org-code-block-from-region (beg end &optional results-switches inline)
  "Copiado de org-babel-examplify-region"
  (interactive "*r")
  (let ((maybe-cap
	 (lambda (str)
	   (if org-babel-uppercase-example-markers (upcase str) str))))
    (if inline
	(save-excursion
	  (goto-char beg)
	  (insert (format org-babel-inline-result-wrap
			  (delete-and-extract-region beg end))))
      (let ((size (count-lines beg end)))
	(save-excursion
	  (cond ((= size 0))	      ; do nothing for an empty result
		(t
		 (goto-char beg)
		 (insert (if results-switches
			     (format "%s%s\n"
				     (funcall maybe-cap "#+begin_src")
				     results-switches)
			   (funcall maybe-cap "#+begin_src\n")))
		 (let ((p (point)))
		   (if (markerp end) (goto-char end) (forward-char (- end beg)))
		   (org-escape-code-in-region p (point)))
		 (insert (funcall maybe-cap "#+end_src\n")))))))))
#+end_src



* Apariencia
** =mode-icons=
Cambia la descripción del modo por un icono en la modeline
   #+begin_src lisp
(use-package mode-icons
  :defer 1
  :ensure t
  :config
  (mode-icons-mode)
)
   #+end_src

   #+RESULTS:

** =origami=

   #+begin_src emacs-lisp

(use-package origami
  :defer 1
  :ensure t
  :config
 (setq origami-show-fold-header nil)
 (setq origami-fold-replacement "⤵⤵⤵")
 (set-face-attribute 'origami-fold-header-face nil :box nil :background 'unspecified)
 (set-face-attribute 'origami-fold-replacement-face nil :box '(:line-width 1 :color unspecified) :background 'unspecified)

  :bind
      (("<C-return>" . origami-recursively-toggle-node))
      (("<C-S-return>" . origami-reset))
      (("<M-return>" . origami-close-all-nodes))



)

   #+end_src


#+begin_src emacs-lisp
(use-package origami-predef
  :ensure t
  :defer 1
  :config

  (origami-predef-global-mode 1)

  (defun origami-predef-javascript()
    "Close some predefined patterns, useful in javascript."
    (interactive)
    (origami-predef-apply-patterns (list
                                    "^\\s-*[[:alnum:]]+([()[:alnum:], >=]*)\\s-*{"
                                    "^\\s-*function\\s-*[[:alnum:]]+([()[:alnum:], >=]*)\\s-*{"
                                    )))
  ;;(add-hook 'js2-mode-hook #'origami-predef-javascript)

  (defun origami-predef-lisp()
    "Close some predefined patterns, useful in elisp."
    (interactive)
    (origami-predef-apply-patterns '("defcustom" "defvar" "defface" "defgroup" "defun .*--")))
  ;;(add-hook 'emacs-lisp-mode-hook #'origami-predef-lisp)

  (defun origami-predef-java()
    "Close some predefined patterns, useful in java."
    (interactive)
    (origami-predef-apply-patterns '("private .*{" "protected .*{")))
  ;;(add-hook 'java-mode-hook #'origami-predef-java)

  (defun origami-predef-sql()
    "Close some predefined patterns, useful in SQL."
    (interactive)
    (origami-predef-apply-patterns '("create .*table" "create .*view" "begin")))
  (add-hook 'sql-mode-hook #'origami-predef-sql)


  (defun origami-predef-gift()
    "Close some predefined patterns, useful in gift-mode (https://github.com/csrhodes/gift-mode)"
    (interactive)
    (origami-predef-apply-patterns "{" ))
  (add-hook 'gift-mode-hook #'origami-predef-gift)
  
  (origami-predef-global-mode 1))
#+end_src

#+RESULTS:



** =imenu=
Reduzco el tamaño de todas las fuentes de =imenu=
   #+begin_src emacs-lisp
(use-package imenu-list
  :defer 1
  :ensure t
  :config 
   (set-face-attribute 'imenu-list-entry-face nil :height  0.8)
   (add-hook 'imenu-list-major-mode-hook (lambda () (message "IMENU MAJOR MODE")(linum-mode 1))))
   #+end_src

   #+RESULTS:


** /Modeline/, =mini-frame= y =feebleline=
Mi línea de estado (modeline)
#+begin_src emacs-lisp
(setq my-mode-line-format
              (list
               " "
               mode-line-modified
               " %[" mode-line-buffer-identification " %] "
               " | " '(vc-mode vc-mode)
               " | %m %n "
               " | %IB %Z"
               " | %l:%c %p"
               ;;" | " '(:eval (format "Point:%d" (point)))
               mode-line-end-spaces
               ) )

(setq-default mode-line-format my-mode-line-format)
#+end_src


Pone en lo alto el minibuffer. No sé si me gusta, funciona algo raro, y lo mismo no va bien con =febbleline= y =mini-modeline=.
#+begin_src lisp
(use-package mini-frame
  :ensure t
  :defer 1
  :config
  (mini-frame-mode))
#+end_src

Prefiero =mini-modeline= para quitar la modeline.
#+begin_src lisp
(use-package    feebleline
  :ensure       t
  :config
  (setq feebleline-msg-functions
        '((feebleline-line-number         :post "" :fmt "%5s")
          (feebleline-column-number       :pre ":" :fmt "%-2s")
          (feebleline-file-directory      :face feebleline-dir-face :post "")
          (feebleline-file-or-buffer-name :face font-lock-keyword-face :post "")
          (feebleline-file-modified-star  :face font-lock-warning-face :post "")
          (feebleline-git-branch          :face feebleline-git-face :pre " : ")
          (feebleline-project-name        :align right)))
  (feebleline-mode 1))
#+end_src


#+begin_src lisp
(use-package    mini-modeline
  :ensure       t
  :defer 10
  :config
  (setq mini-modeline-enhance-visual t)
  (setq mini-modeline-r-format my-mode-line-format)
  (my-centaur-tabs-font-sizes 100)
  (mini-modeline-mode t))
#+end_src


** Cursor
Cursor como barra vertical
#+begin_src emacs-lisp
(set-default 'cursor-type 'bar)
(set-default 'cursor-in-non-selected-windows 'hollow)
(defun my/change-cursor-when-overwrite ()
  (setq cursor-type (if overwrite-mode 'box 'bar)))
(add-hook 'overwrite-mode-hook #'my/change-cursor-when-overwrite)
(blink-cursor-mode 1)
(setq blink-cursor-blinks 0)
#+end_src

#+RESULTS:
: 0

** Líneas vacías al final, como =vim=
Creo que esto se hace mejor con la variable =indicate-empty-lines=
#+begin_src lisp
(add-hook 'prog-mode-hook 'vim-empty-lines-mode)
(add-hook 'org-mode-hook 'vim-empty-lines-mode)
#+end_src

** Nivel de indentación
#+begin_src emacs-lisp
(setq highlight-indent-guides-method 'fill)
#+end_src


** Indicación de cambios de /git/
Utilizo /git/ para casi todos mis ficheros. =diff-hl=  marca en el margen izquierdo las líneas cambiadas, añadidas o borradas respecto de la versión de la rama actual. 

#+BEGIN_SRC emacs-lisp
(use-package diff-hl
  :defer 1
  :ensure t
  :config

  (use-package diff-hl-margin)
  

  (use-package diff-hl-dired
    :config
    (diff-hl-dired-mode 1))

  (use-package diff-hl-flydiff
    :config
    (diff-hl-flydiff-mode 1))

  (use-package diff-hl-show-hunk
    :config
    (require 'diff-hl-show-hunk-posframe)

    (defun diff-hl-show-hunk-toggle-backend()
      (interactive)
      (if (eq diff-hl-show-hunk-function 'diff-hl-show-hunk-posframe)
          (setq diff-hl-show-hunk-function 'diff-hl-show-hunk-inline-popup)
        (setq diff-hl-show-hunk-function 'diff-hl-show-hunk-posframe)))
 
    (defun diff-hl-show-hunk-toggle-style()
      (interactive)
      (if diff-hl-show-hunk-inline-popup-hide-hunk
          (progn
            (setq diff-hl-show-hunk-inline-popup-hide-hunk nil)
            (setq diff-hl-show-hunk-inline-popup-smart-lines t))
        (setq diff-hl-show-hunk-inline-popup-hide-hunk t)
        (setq diff-hl-show-hunk-inline-popup-smart-lines nil)))

    (setq diff-hl-show-hunk-inline-popup-hide-hunk t)
    (setq diff-hl-show-hunk-inline-popup-smart-lines nil)
    
    (global-diff-hl-show-hunk-mouse-mode 1))
  
  (add-hook 'magit-post-refresh-hook 'diff-hl-magit-post-refresh)
  (global-diff-hl-mode 1))


#+END_SRC

#+RESULTS:
: t


** Saltos de página
Mostrar =^L= (saltos de página) como una línea horizontal
#+begin_src emacs-lisp
(use-package page-break-lines
  :defer t
  :ensure t
  :config
  (global-page-break-lines-mode))
#+end_src

** Modo proyección o modo trabajo
Utilizo emacs de dos modos muy distintos: para trabajar y para proyectar en clase. Estas dos funciones cambian opciones de visualización adecuadas para cada ocasión.
He deshabilitado la indentación en los modos de programación, ralentiza bastante en los ficheros grandes.
#+begin_src emacs-lisp
(defun bonito-para-proyector()
  (interactive)
  (bonito-para-codigo)
  (adaptive-wrap-prefix-mode 1)
  (toggle-truncate-lines -1)
  (highlight-indent-guides-mode 0)
  (if (>= emacs-major-version 26)
      (display-line-numbers-mode 0))
  (org-display-inline-images))

(defun bonito-para-org()
  (interactive)
  (bonito-para-proyector)
  (org-bullets-mode 1)
  (electric-pair-local-mode 1))


(defun bonito-para-codigo()
  (interactive)
  (toggle-truncate-lines 1)
  (highlight-indent-guides-mode 1)
  (toggle-word-wrap 1)
  (if (>= emacs-major-version 26)
      (display-line-numbers-mode 1))
  (auto-highlight-symbol-mode 1)
  (electric-pair-local-mode 1)
  (origami-mode 1))

(defun bonito-para-latex()
  (interactive)
  (toggle-truncate-lines -1)
  (setq magic-latex-enable-suscript nil)
  (company-auctex-init)
  (magic-latex-buffer 1))



(add-hook 'prog-mode-hook 'bonito-para-codigo)
(add-hook 'text-mode-hook 'bonito-para-proyector)
(add-hook 'org-mode-hook 'bonito-para-org)
(add-hook 'LaTeX-mode-hook 'bonito-para-latex)
#+end_src


** /Fringes/
Prefiero ocultar las flechas que indican que una línea se sale de la pantalla, y solo mostrar las de la derecha.

#+begin_src emacs-lisp
(if (display-graphic-p)
  (fringe-mode '(0 . nil)))
#+end_src

** Temas
Tengo dos temas, claro y oscuro. El tema =alvaro= cambia algunos tamaños de letra (no colores).

Hay que marcar los temas como seguros. Para eso se deben registrar sus huellas en =custom-safe-themes= (lo he copiado del fichero =custom-file.el=).



#+begin_src emacs-lisp
(setq  custom-safe-themes  (quote
                            ("fc6697788f00629cd01f4d2cc23f1994d08edb3535e4c0facef6b7247b41f5c7"
                             "d916b686ba9f23a46ee9620c967f6039ca4ea0e682c1b9219450acee80e10e40" 
                             "a63355b90843b228925ce8b96f88c587087c3ee4f428838716505fd01cf741c8"
                             "6e219d6b6a3f7e22888b203fd5492e12133ba40512be983858f05b42806fa573"
                             "1b8d67b43ff1723960eb5e0cba512a2c7a2ad544ddb2533a90101fd1852b426e" 
                             "b53db91fd0153783f094a2d5480119824b008f158e07d6b84d22f8e6b063d6e2" 
                             default)))

(defvar my-dark-theme 'my-tangotango)
(defvar my-light-theme 'intellij)


(defun tema-claro()
  (interactive)
  (disable-theme my-dark-theme)
  (load-theme my-light-theme)
  (load-theme 'alvaro t))

(defun tema-oscuro ()
  (interactive)
  (disable-theme my-light-theme)
  (load-theme my-dark-theme t)
  (load-theme 'alvaro t))

#+end_src

#+RESULTS:
: tema-oscuro


Pongo uno detrás de otro para "limpiar" lo que haya podido quedarse de alguna customización. Por lo visto, un tema siempre añade cambios, pero al quitarse no se deshacen  completamente. Algunas configuraciones solo se tienen en cuenta al reiniciar /Emacs/ o al reaplicar un modo: por ejemplo, los colores de =highlight-indent-guides= necesitan reabrir el buffer.
#+begin_src lisp
(tema-claro)
(tema-oscuro)
#+end_src














* Escritorio
Grabar la disposición de bufers y ventanas para la siguiente sesión. Lo hago al final, para que se carguen ahora los buferes previos.
#+begin_src emacs-lisp
  (save-place-mode 1)

  (use-package desktop
    :defer 1
    :ensure t
    :config
    (setq desktop-load-locked-desktop t)
    (setq desktop-save t)
    (desktop-save-mode 1)
    ;(when (y-or-n-p-with-timeout "¿Cargar el escritorio (5 segundos)? " 5 t)
    ;    (desktop-read))
  )

#+end_src

#+RESULTS:

* KMS

Manual
https://raw.githubusercontent.com/Wind4/vlmcsd/master/man/vlmcsd.7

Servicio de ubuntu
https://blog.thirdechelon.org/2019/06/vlmcsd-on-ubuntu-18-04/

Esta parece la fuente
https://github.com/kkkgo/vlmcsd

Binario de raspberry
https://github.com/Wind4/vlmcsd/issues/28

Docker de raspberry
https://github.com/elarkasi/raspi-kms


* Futuras adiciones



  (use-package highlight-indent-guides
  :hook ((prog-mode text-mode conf-mode) . highlight-indent-guides-mode)
  :init
  (setq highlight-indent-guides-method 'character
        highlight-indent-guides-supress-auto-error t))

(use-package tree-sitter-langs)
(use-package tree-sitter
  :config
  (require 'tree-sitter-langs)
  (global-tree-sitter-mode)
  (add-hook 'tree-sitter-after-on-hook #'tree-sitter-hl-mode))
(provide 'treesitter-rcp)



vi-tilde-fringe

dired-kill-when-opening-new-dired-buffer.


        


  En https://github.com/caisah/emacs.dz hay una colección de configuraciones /Emacs/ muy interesantes. Esta es una lista de paquetes a investigar, extraída de los que se usan en esas configuraciones



;; If we're talking about fonts, you gotta have emojis
(use-package emojify
  :hook (after-init . global-emojify-mode))


global-set-key (kbd "C-c h w") 'helm-wikipedia-suggest)
    (global-set-key (kbd "C-c h g") 'helm-google-suggest)

(use-package peep-dired)

;; Copy and paste files
(use-package dired-ranger
  :bind (:map dired-mode-map
              ("W" . dired-ranger-copy)
              ("X" . dired-ranger-move)
              ("Y" . dired-ranger-paste)))

(setq dired-open-extensions '(("gif" . "sxiv")
                              ("jpg" . "sxiv")
                              ("png" . "sxiv")
                              ("mkv" . "mpv")
                              ("mp4" . "mpv")))



- good-scroll
- emacs-hl-block-mode
- helm-xref
- helm-icons
- (use-package json-navigator) aa
- 
--  (fast-but-imprecise-scrolling t)
- find-trace-paths
-  (add-hook 'org-babel-after-execute-hook 'org-redisplay-inline-images) 
- https://github.com/manateelazycat/awesome-tray
- http://ergoemacs.org/emacs/emacs_list_and_set_font.html
- https://github.com/lastquestion/explain-pause-mode
- https://github.com/Borderliner/RME
- feebleline
- vlfi ficheros grandes
- asciinema
- company-quickhelp-terminal
- aftersave-add-hook
- (setq org-download-method 'attach)
- bandwich
- https://github.com/jakubroztocil/httpie/blob/master/README.rst
- https://github.com/honmaple/emacs-maple-minibuffer/blob/master/README.org
- http://blog.binchen.org/posts/effective-git-blame-in-emacs.html
- https://github.com/astoff/digestif/blob/master/README.md
- Elfeed
- Zoom-frm
- greader
- https://zge.us.to/emacs.d.html
- change-inner
- magithub
- google-this
- emamux
- https://gitlab.com/LazyLama/emacs_latex_class
- https://github.com/patrickt/emacs/blob/master/init.el
- https://melpa.org/#/helm-file-preview
- openwith
- calctex
- dot-mode
- (global-eldoc-mode -1)
- didyoumean.el 
- BAT cat
- statusbar.el
- dired-single
- https://www.wezm.net/technical/2019/10/useful-command-line-tools/




#+begin_src lisp
(add-to-list 'load-path "~/.emacs.d/mis-paquetes/lsp-latex")
(require 'lsp-latex)
;; "texlab" must be located at a directory contained in `exec-path'.
;; If you want to put "texlab" somewhere else,
;; you can specify the path to "texlab" as follows:
;; (setq lsp-latex-texlab-executable "/path/to/texlab")

(with-eval-after-load "tex-mode"
 (add-hook 'tex-mode-hook 'lsp)
 (add-hook 'latex-mode-hook 'lsp))

;; For YaTeX
(with-eval-after-load "yatex"
 (add-hook 'yatex-mode-hook 'lsp))
#+end_src

* usettf

Actualmente desactivado

#+BEGIN_SRC lisp
  (setq use-ttf-default-ttf-fonts '("/.emacs.d/fonts/SourceCodeVariable-Roman.ttf"
                                    "/.emacs.d/fonts/NotoSansMono-Regular.ttf"))
  (setq use-ttf-default-ttf-font-name "Source Code Variable")
  (call-interactively #'use-ttf-install-fonts)
  (call-interactively #'use-ttf-set-default-font)
#+END_SRC



* Opciones Latex para el PDF :noexport:
# COLORES
#+latex_header: \usepackage[usenames,dvipsnames]{color} % Required for custom colors

# LISTADOS LATEX
#+latex_header: \renewcommand{\ttdefault}{pcr} % MONOESPACIO CON NEGRITA
#+latex_header: \usepackage{listings}
#+latex_header: \usepackage{listingsutf8}
#+latex_header: \usepackage{indentfirst}
#+latex_header: \lstset{frame=single,inputencoding=utf8,basicstyle=\scriptsize\ttfamily,showstringspaces=false,numbers=none}
#+latex_header: \definecolor{MyDarkGreen}{rgb}{0.0,0.4,0.0} % This is the color used for comments
#+latex_header: \lstset{ breaklines=true, postbreak=\mbox{\textcolor{red}{$\hookrightarrow$}\space}, keywordstyle=\bfseries, keywordstyle=[1]\color{Blue}\bfseries,  keywordstyle=[2]\color{Purple}\bfseries,  keywordstyle=[3]\color{Blue}\underbar,   identifierstyle=,   commentstyle=\color{MyDarkGreen},   stringstyle=\color{Purple},   showstringspaces=false,   tabsize=2,   morecomment=[l][\color{Blue}]{...} }
#+latex_header: \lstset{literate=  {á}{{\'a}}1 {é}{{\'e}}1 {í}{{\'i}}1 {ó}{{\'o}}1 {ú}{{\'u}}1   {Á}{{\'A}}1 {É}{{\'E}}1 {Í}{{\'I}}1 {Ó}{{\'O}}1 {Ú}{{\'U}}1   {à}{{\`a}}1 {è}{{\`e}}1 {ì}{{\`i}}1 {ò}{{\`o}}1 {ù}{{\`u}}1   {À}{{\`A}}1 {È}{{\'E}}1 {Ì}{{\`I}}1 {Ò}{{\`O}}1 {Ù}{{\`U}}1   {ä}{{\"a}}1 {ë}{{\"e}}1 {ï}{{\"i}}1 {ö}{{\"o}}1 {ü}{{\"u}}1   {Ä}{{\"A}}1 {Ë}{{\"E}}1 {Ï}{{\"I}}1 {Ö}{{\"O}}1 {Ü}{{\"U}}1   {â}{{\^a}}1 {ê}{{\^e}}1 {î}{{\^i}}1 {ô}{{\^o}}1 {û}{{\^u}}1   {Â}{{\^A}}1 {Ê}{{\^E}}1 {Î}{{\^I}}1 {Ô}{{\^O}}1 {Û}{{\^U}}1   {œ}{{\oe}}1 {Œ}{{\OE}}1 {æ}{{\ae}}1 {Æ}{{\AE}}1 {ß}{{\ss}}1   {ű}{{\H{u}}}1 {Ű}{{\H{U}}}1 {ő}{{\H{o}}}1 {Ő}{{\H{O}}}1   {ç}{{\c c}}1 {Ç}{{\c C}}1 {ø}{{\o}}1 {å}{{\r a}}1 {Å}{{\r A}}1   {€}{{\euro}}1 {£}{{\pounds}}1 {«}{{\guillemotleft}}1   {»}{{\guillemotright}}1 {ñ}{{\~n}}1 {Ñ}{{\~N}}1 {¿}{{?`}}1 } 

# OPCIONES DE PÁGINA DE LATEX
#+LATEX_CLASS: article
#+LATEX_CLASS_OPTIONS:
#+LATEX_HEADER: \usepackage[margin=2.5cm,includeheadfoot,includehead,includefoot]{geometry} 
#+LATEX_HEADER: \hypersetup{colorlinks,linkcolor=black}

# CABECERA Y PIE LATEX
#+LATEX_HEADER: \usepackage{fancyhdr}
#+LATEX_HEADER: \pagestyle{fancyplain}
#+LATEX_HEADER: \chead{}
#+LATEX_HEADER: \lhead{}
#+LATEX_HEADER: \rhead{}
#+LATEX_HEADER: \cfoot{}
#+LATEX_HEADER: \lfoot{alvarogonzalezsotillo@gmail.com}
#+LATEX_HEADER: \rfoot{\thepage}

* localizar android
** bluetooth discovery https://code.tutsplus.com/tutorials/create-a-bluetooth-scanner-with-androids-bluetooth-api--cms-24084
https://developer.android.com/reference/android/bluetooth/BluetoothAdapter.html

** umts
https://stackoverflow.com/questions/20981074/ecgi-and-cgi-for-lte-and-gsm-networks
https://comunidad.orange.es/t5/Android/TRUCO-Descubriendo-todo-lo-relacionado-con-la-cobertura-en-Android-post-100-geek/td-p/290749
https://gabinetejuridicotecnologicojuandemeseguer.es/localizacion-posicionamiento-moviles-mediante-sistemas-moviles-gps
https://ltve.wordpress.com/2015/09/30/android-con-api-opencellid-json-geolocalizar-bts/
https://developer.android.com/reference/android/telephony/TelephonyManager.html#getAllCellInfo()

** wifi
https://developer.android.com/reference/android/net/wifi/ScanResult.html

** location
https://developer.android.com/training/location/retrieve-current#java


* Tele samsung
https://www.askvg.com/secret-service-codes-for-sony-and-lg-tv/



    Service Menu Code For Samsung TV:

There are many codes available for accessing service menu in various Samsung TV models. You can try following codes one by one until you get access to service menu:

First try following method. It works in all new Samsung TV series:

Method 1: DO NOT power off your TV. Keep it ON and then press following buttons quickly on your TV remote:

Mute + 1 + 1 + 9 + Enter

Here "Enter" button is the OK/Select button present in the center of Left/Right/Top/Bottom buttons. It'll open secret service menu which allows you to turn on/off Hospitality Mode/Hotel Mode, etc.

Other Methods:

If above mentioned method doesn't work for you, try following methods:

First power off your TV using the remote, then press following buttons quickly on the remote:

Info + Settings + Mute + Power
Mute + 1 + 8 + 2 + Power
Mute + 1 + 1 + 9 + Power
Display/Info + Menu + Mute + Power
Display/Info + P.STD + Mute + Power
P.STD + Help + Sleep + Power
P.STD + Menu + Sleep + Power
Sleep + P.STD + Mute + Power

You can press Channel Up/Down buttons to navigate between available options. To change their values, press Volume Up/Down buttons.

